//Remove a Component

//Libraries
import "../evl/library/artifact.eol";
import "../evl/library/association.eol";
import "../evl/library/classifier.eol";
import "../evl/library/component.eol";
import "../evl/library/element.eol";
import "../evl/library/interaction.eol";
import "../evl/library/interactionFragment.eol";
import "../evl/library/interactionOperand.eol";
import "../evl/library/combinedFragment.eol";
import "../evl/library/interface.eol";
import "../evl/library/lifeline.eol";
import "../evl/library/manifestation.eol";
import "../evl/library/message.eol";
import "../evl/library/node.eol";
import "../evl/library/operation.eol";
import "../evl/library/package.eol";
//HELPER
import "../evl/library/HELPER.eol";

//Component to be removed
var target = Component.all->selectOne(c | c.getName() == targetComponentName);

if(target.isUndefined())
	throw "[ERROR]: Component " + targetComponentName + " is undefined";

//Given Lifeline move everything in component and remove empty entities 
for (lf : Lifeline in target.getLifelines()){
	var messageMap : Map = lf.getIncomingMessages();
	var listOfMessages = messageMap.values().first();
	
	var seenOperations : Set;
	
	for(messageInUse: Message in listOfMessages ){
		var op : Operation = messageInUse.getSignature();
		
		if(not seenOperations.includes(messageInUse.getInteraction().getName()+"_"+op.getName())){
			seenOperations.add(messageInUse.getInteraction().getName()+"_"+op.getName());
				
			// Recupera Component dove muovere OP
			var senderLf : Lifeline = messageInUse.getSendEvent().getCovered();
			var senderComponent: Component = senderLf.getRepresents().getType();
			// move the operation
			senderComponent.moveOperation(op);
			op.dynamicFixing(target);
		}			
	}
		
	lf.destroy();
}

// Clean Dlepoyment diagram
var nodesToDestroy : Sequence = target.getNodes();
for(n : Node in nodesToDestroy){
	for (a: Artifact in n.getArtifact() ){
		a.destroy();
	}
	for (cp in n.getCommunicationPaths()){
		var memberEnds = cp.getMemberEnds();
		for(property : Property in memberEnds){
			if(property.getType == n){
				property.destroy();
			}
		}
		cp.destroy();
	}
	n.destroy();
}


// Destroy component
target.destroy();
"Remove Component Done".println();
